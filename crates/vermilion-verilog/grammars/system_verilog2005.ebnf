(* SPDX-License-Identifier: BSD-3-Clause *)
(* EBNF grammar for SystemVerilog 2005 (IEEE 1800-2005) *)
(*
	NOTE 1: Embedded spaces are illegal.
	NOTE 2: A `simple_identifier`, `c_identifier`, and `arrayed_reference` shall start with [a-zA-Z_],
			shall have at lease one character, and shall not have any spaces.
	NOTE 3: The dollar (`$`) character in a `system_tf_identifier` shall not be followed by
			`white_space`. A `system_tf_identifier` shall not be escaped.
	NOTE 4: EOF (End Of File) is valid white space.
	NOTE 5: The unsigned number of fixed-point number in `time_literal` shall not be followed by one
			or more `white_space`.
	NOTE 6: `implicit_class_handle` shall only appear within the scope of a `class_declaration` or
			out-of-block method declaration.
	NOTE 7: In any one declaration, only one of `protected` or `local` is allowed, only one of
			`rang` or `randc` is allowed, and `static` and/or `virtual` can appear only once.
	NOTE 8: `dpi_function_proto` return types are restricted to small values, per
			IEEE 1800-2005 ยง26.4.5.
	NOTE 9: Formals of `dpi_function_proto` and `dpi_task_proto` cannot use pass-py-reference mode,
			and class types cannot be passed at all; for the complete set of restrictions see
			IEEE 1800-2005 ยง26.4.6.
	NOTE 10: The apostrophe (`'`) in `unbased_unsized_literal` shall not be followed by one or more
			 `white_space`.
	NOTE 11: In `packed_dimension`, `unsized_dimension` is permitted only in declarations of import
			 DPI functions; see `dpi_function_proto`.
	NOTE 12: When a packed dimension is used with the `struct` or `union` keyword, the `packed` keyword
			 shall also be used.
	NOTE 13: A charge strength shall only be used with the `trireg` keyword. WWhen the `vectored` or
			 `scalared` keyword is used, there shall be at least one packed dimension.
	NOTE 14: In a `data_declaration` that is not within the procedural context, it shall be illegal
			 to use the `automatic` keyword. In a `data_declaration`, it shall be illegal to omit the
			 explicit `data_type` before a `list_of_variable_decl_assignments` unless the `var` keyword
			 is used.
	NOTE 15: It shall be legal to omit the `covergroup_variable_identifier` from a covergroup instantiation
			 only if this implicit instantiation is within a class that has no other instantiation of
			 the covergroup.
	NOTE 16: The `.*` token shall appear at most once in a list of port connections.
	NOTE 17: A `timeunits_declaration` shall be legal as a `non_port_module_item`, `non_port_interface_item`,
			 `non_port_program_item`, `package_item`, or `class_item` only if it repeats and matches
			 a previous `timeunits_declaration` within the same time scope.
	NOTE 18: In a `multiple_concatenation`, it shall be illegal for the multiplier not to be a
			 `constant_expression` unless the type of the concatenation is `string`.
	NOTE 19: In a shallow copy, the expression must evaluate to an object handle.
	NOTE 20: It shall be legal to use the `$` primary in an `open_value_range` of the form
			 `[` expression `:` `$` `]` or `[` `$` `:` expression `]`.
	NOTE 21: `{}` shall only be legal in the context of a queue.
	NOTE 22: The `$` primary shall be legal only in a select for a queue variable or in an
			 `open_value_range`.
	NOTE 23: A `type_identifier` shall be legal as an `enum_base_type` if it denotes an `integer_atom_type`
			 with which an additional packed dimension is not permitted, or an `integer_vector_type`.
	NOTE 24: In a `constant_function_call`, all arguments shall be `constant_expression`s.
	NOTE 25: The `list_of_port_declarations` syntax is explained in IEEE 1800-2005 ยง19.8, which also
			 imposes various semantic restrictions, e.g. a `ref` port must be of a variable type and
			 an `inout` port must not be. It shall be illegal to initialize a port that is not a
			 variable `output` port.
	NOTE 26: It shall be legal to declare a `void` `struct_union_member` only within tagged unions.
	NOTE 27: An expression that is used as the argument in a `type_reference` shall not contain
			 any hierarchical references or references to elements of dynamic objects.
	NOTE 28: When a `type_reference` is used in a net declaration, it shall be preceded by a net
			 type keyword; and when it is used in a variable declaration is shall be preceded by
			 the `var` keyword.
	NOTE 29: It shall be legal to use a `type_reference` `constant_primary` as the `casting_type`
			 in a static cast. It shall be illegal for a `type_reference` `constant_primary` to be
			 used with any operators except the equality/inequality and case equality/inequality
			 operators.
	NOTE 30: A `streaming_concatenation` expression shall not be nested within another
			 `variable_lvalue`. A `streaming_concatenation` shall not be the target of the increment
			 or decrement operator, nor the target of any assignment operator except the simple (`=`)
			 or nonblocking assignment (`<=`) operator.
	NOTE 31: Within an `interface_declaration`, it shall only be legal for a `module_or_interface_or_generate_item`
			 to be an `interface_or_generate_item`. Within a `module_declaration`, except when also
			 within an `interface_declaration`, it shall only be legal for a `module_or_interface_or_generate_item`
			 to be a `module_or_generate_item`.
	NOTE 32: A `genvar_identifier` shall be legal in a `constant_primary` only within a `genvar_expression`.
	NOTE 33: When a `net_port_type` contains a `data_type`, it shall only be legal to omit the
			 explicit `net_type` when declaring an `inout` port.
	NOTE 34: In a `tf_port_item`, it shall be illegal to omit the explicit `port_identifier` except
			 within a `function_prototype` or `task_prototype`.
	NOTE 35: In a `constant_assignment_pattern_expression`, all member expressions shall be constant
			 expression.
	NOTE 36: It shall be illegal to omit the parenthesis in a `tf_call` unless the subroutine is a
			 task, void function, or class method. If the subroutine is a nonvoid class function
			 method, it shall be illegal to omit the parenthesis if the call is directly recursive.
	NOTE 37: It shall be illegal for a `program_generate_item` to include any item that would be
			 illegal in a `program_declaration` outside of a `program_generate_item`.
*)

(* See NOTE 4 *)
white_space =
	" " | "\t" | "\n"
	;

variable_identifier =
	identifier
	;

udp_identifier =
	identifier
	;

type_identifier =
	identifier
	;

topmodule_identifier =
	identifier
	;

terminal_identifier =
	identifier
	;

tf_identifier =
	identifier
	;

task_identifier =
	identifier
	;

(* See NOTE 3 *)
system_tf_identifier =
	"$", a-zA-Z0-9_$, { a-zA-Z0-9_$ }
	;

specparam_identifier =
	identifier
	;

(* See NOTE 2 *)
simple_identifier =
	a-zA-Z_, { a-zA-Z0-9_$ }
	;

signal_identifier =
	identifier
	;

sequence_identifier =
	identifier
	;

ps_type_identifier =
	[ package_scope ], type_identifier
	;

ps_sequence_identifier =
	[ package_scope ], sequence_identifier
	;

ps_property_identifier =
	[ package_scope ], property_identifier
	;

ps_parameter_identifier =
	[ package_scope ], parameter_identifier |
	{ generate_block_identifier, [ "[", constant_expression, "]" ], "." }, parameter_identifier
	;

ps_or_hierarchical_tf_identifier =
	[ package_scope ], tf_identifier |
	hierarchical_tf_identifier
	;

ps_or_hierarchical_net_identifier =
	[ package_scope ], net_identifier |
	hierarchical_net_identifier
	;

ps_identifier =
	[ package_scope ], identifier
	;

ps_covergroup_identifier =
	[ package_scope ], covergroup_identifier
	;

ps_class_identifier =
	[ package_scope ], class_identifier
	;

property_identifier =
	identifier
	;

program_identifier =
	identifier
	;

production_identifier =
	identifier
	;

port_identifier =
	identifier
	;

parameter_identifier =
	identifier
	;

package_scope =
	package_identifier, "::" |
	"$unit", "::"
	;

package_identifier =
	identifier
	;

output_port_identifier =
	identifier
	;

net_identifier =
	identifier
	;

module_identifier =
	identifier
	;

modport_identifier =
	identifier
	;

method_identifier =
	identifier
	;

member_identifier =
	identifier
	;

library_identifier =
	identifier
	;

instance_identifier =
	identifier
	;

input_port_identifier =
	identifier
	;

inout_port_identifier =
	identifier
	;

interface_instance_identifier =
	identifier
	;

interface_identifier =
	identifier
	;

index_variable_identifier =
	identifier
	;

identifier =
	simple_identifier |
	escaped_identifier
	;

hierarchical_variable_identifier =
	hierarchical_identifier
	;

hierarchical_tf_identifier =
	hierarchical_identifier
	;

hierarchical_task_identifier =
	hierarchical_identifier
	;

hierarchical_parameter_identifier =
	hierarchical_identifier
	;

hierarchical_net_identifier =
	hierarchical_identifier
	;

hierarchical_identifier =
	[ "$root", "." ], { identifier, constant_bit_select, "." }, identifier
	;

hierarchical_event_identifier =
	hierarchical_identifier
	;

hierarchical_dynamic_array_variable_identifier =
	hierarchical_variable_identifier
	;

hierarchical_block_identifier =
	hierarchical_identifier
	;

genvar_identifier =
	identifier
	;

generate_block_identifier =
	identifier
	;

function_identifier =
	identifier
	;

formal_identifier =
	identifier
	;

escaped_identifier =
	"\\", { ? Any_ASCII_Character_Except_White_Space ? }, white_space
	;

enum_identifier =
	identifier
	;

dynamic_array_variable_identifier =
	variable_identifier
	;

cover_point_identifier =
	identifier
	;

covergroup_variable_identifier =
	variable_identifier
	;

covergroup_identifier =
	identifier
	;

constraint_identifier =
	identifier
	;

const_identifier =
	identifier
	;

config_identifier =
	identifier
	;

clocking_identifier =
	identifier
	;

class_variable_identifier =
	variable_identifier
	;

class_identifier =
	identifier
	;

cell_identifier =
	identifier
	;

(*  See NOTE 2 *)
c_identifier =
	a-zA-Z_, { a-zA-Z0-9_ }
	;

bin_identifier =
	identifier
	;

block_identifier =
	identifier
	;

array_identifier =
	identifier
	;

comment_text =
	{ ? Any_ASCII_character ? }
	;

block_comment =
	"/*", comment_text, "*/"
	;

one_line_comment =
	"//", comment_text, "\n"
	;

comment =
	one_line_comment |
	block_comment
	;

attr_name =
	identifier
	;

attr_spec =
	attr_name, [ "=", constant_expression ]
	;

attribute_instance =
	"(*", attr_spec, { ",", attr_spec }, "*)"
	;

string_literal =
	'"', { ? Any_ASCII_Characters ? }, '"'
	;

unbased_unsized_literal =
	"'0" |
	"'1" |
	(* See NOTE 10 *)
	"'", z_or_x
	;

z_digit =
	"z" | "Z" | "?"
	;

x_digit =
	"x" | "X"
	;

hex_digit =
	x_digit | z_digit | "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" |
	"a" | "b" | "c" | "d" | "e" | "f" |
	"A" | "B" | "C" | "D" | "E" | "F"
	;

octal_digit =
	x_digit | z_digit | "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7"
	;

binary_digit =
	x_digit | z_digit | "0" | "1"
	;

decimal_digit =
	"0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
	;

non_zero_decimal_digit =
	"1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
	;

(* See NOTE 1 *)
hex_base =
	"'", [ "s" | "S" ], "h" |
	"'", [ "s" | "S" ], "H"
	;

(* See NOTE 1 *)
octal_base =
	"'", [ "s" | "S" ], "o" |
	"'", [ "s" | "S" ], "O"
	;

(* See NOTE 1 *)
binary_base =
	"'", [ "s" | "S" ], "b" |
	"'", [ "s" | "S" ], "B"
	;

(* See NOTE 1 *)
decimal_base =
	"'", [ "s" | "S" ], "d" |
	"'", [ "s" | "S" ], "D"
	;

(* See NOTE 1 *)
hex_value =
	hex_digit, { "_" | hex_digit }
	;

(* See NOTE 1 *)
octal_value =
	octal_digit, { "_" | octal_digit }
	;

(* See NOTE 1 *)
binary_value =
	binary_digit, { "_" | binary_digit }
	;

(* See NOTE 1 *)
unsigned_number =
	decimal_digit, { "_" | decimal_digit }
	;

exp =
	"e" | "E"
	;

(* See NOTE 1 *)
fixed_point_number =
	unsigned_number, ".", unsigned_number
	;

(* See NOTE 1 *)
real_number =
	fixed_point_number |
	unsigned_number, [ ".", unsigned_number ], exp, [ sign ], unsigned_number
	;

(* See NOTE 1 *)
non_zero_unsigned_number =
	non_zero_decimal_digit, { "_" | decimal_digit }
	;

size =
	non_zero_unsigned_number
	;

sign =
	"+" | "-"
	;

hex_number =
	[ size ], hex_base, hex_value
	;

octal_number =
	[ size ], octal_base, octal_value
	;

binary_number =
	[ size ], binary_base, binary_value
	;

decimal_number =
	unsigned_number |
	[ size ], decimal_base, unsigned_number |
	[ size ], decimal_base, x_digit, { "_" } |
	[ size ], decimal_base, z_digit, { "_" }
	;

integral_number =
	decimal_number |
	octal_number |
	binary_number |
	hex_number
	;

number =
	integral_number |
	real_number
	;

binary_module_path_operator =
	"==" | "!=" | "&&" | "||" | "&" | "|" | "^" | "^~" | "~^"
	;

unary_module_path_operator =
	"!" | "~" | "&" | "~&" | "|" | "~|" | "^" | "~^" | "^~"
	;

inc_or_dec_operator =
	"++" | "--"
	;

binary_operator =
	"+" | "-" | "*" | "/" | "%" | "==" | "!=" | "===" | "!==" | "==?" | "!=?" | "&&" | "||" | "**" |
	"<" | "<=" | ">" | ">=" | "&" | "|" | "^" | "^~" | "~^" | ">>" | "<<" | ">>>" | "<<<"
	;

unary_operator =
	"+" | "-" | "!" | "~" | "&" | "~&" | "|" | "~|" | "^" | "~^" | "^~"
	;

variable_lvalue =
	[ implicit_class_handle, "." | package_scope ], hierarchical_variable_identifier, select |
	"{", variable_lvalue, { ",", variable_lvalue }, "}" |
	[ assignment_pattern_expression_type ], assignment_pattern_variable_lvalue |
	(* See NOTE 30 *)
	stream_concatenation
	;

net_lvalue =
	ps_or_hierarchical_net_identifier, constant_select |
	"{",  net_lvalue, { ",", net_lvalue }, "}" |
	[ assignment_pattern_expression_type ], assignment_pattern_net_lvalue
	;

cast =
	casting_type, "'", "(", expression, ")"
	;

constant_cast =
	casting_type, "'", "(", constant_expression, ")"
	;

primary_literal =
	number |
	time_literal |
	unbased_unsized_literal |
	string_literal
	;

constant_select =
	[ { ".", member_identifier, constant_bit_select }, ".", member_identifier ], constant_bit_select,
		[ "[", constant_part_select_range ,"]" ]
	;

constant_bit_select =
	{ "[", constant_expression, "]" }
	;

select =
	[ { ".", member_identifier, bit_select }, ".", member_identifier ], bit_select, [ "[", part_select_range ,"]" ]
	;

bit_select =
	{ "[", expression, "]" }
	;

(* See NOTE 6 *)
implicit_class_handle =
	"this" | "super" | "this", ".", "super"
	;

time_unit =
	"s" | "ms" | "us" | "ns" | "ps" | "fs" | "step"
	;

(* See NOTE 5 *)
time_literal =
	unsigned_number, time_unit |
	fixed_point_number, time_unit
	;

primary =
	primary_literal |
	[ implicit_class_handle, "." | class_scope | package_scope ], hierarchical_identifier, select |
	empty_queue |
	concatenation |
	multiple_concatenation |
	function_subroutine_call |
	"(", mintypmax_expression, ")" |
	cast |
	assignment_pattern_expression |
	streaming_concatenation |
	sequence_method_call |
	(* See NOTE 6 *)
	"this" |
	(* See NOTE 22 *)
	"$" |
	"null"
	;

module_path_primary =
	number |
	identifier |
	module_path_concatenation |
	module_path_multiple_concatenation |
	function_subroutine_call |
	"(", module_path_mintypmax_expression, ")"
	;

constant_primary =
	primary_literal |
	ps_parameter_identifier, constant_select |
	specparam_identifier, [ constant_range_expression ] |
	(* See NOTE 32 *)
	genvar_identifier |
	[ package_scope | class_scope ], enum_identifier |
	constant_concatenation |
	constant_multiple_concatenation |
	constant_function_call |
	"(", constant_mintypmax_expression, ")" |
	constant_cast |
	constant_assignment_pattern_expression |
	(* See NOTE 29 *)
	type_reference
	;

genvar_expression =
	constant_expression
	;

indexed_range =
	expression, "+:", constant_expression |
	expression, "-:", constant_expression
	;

part_select_range =
	constant_range |
	indexed_range
	;

module_path_mintypmax_expression =
	module_path_expression |
	module_path_expression, ":", module_path_expression, ":", module_path_expression
	;

module_path_expression =
	module_path_primary |
	unary_module_path_operator, { attribute_instance }, module_path_primary |
	module_path_expression, binary_module_path_operator, { attribute_instance }, module_path_expression |
	module_path_conditional_expression
	;

module_path_conditional_expression =
	module_path_expression, "?", { attribute_instance }, module_path_expression, ":", module_path_expression
	;

mintypmax_expression =
	expression |
	expression, ":", expression, ":", expression
	;

value_range =
	expression |
	"[", expression, ":", expression, "]"
	;

inside_expression =
	expression, "inside", { open_range_list }
	;

tagged_union_expression =
	"tagged", member_identifier, [ expression ]
	;

expression =
	primary |
	unary_operator, { attribute_instance }, primary |
	inc_or_dec_expression |
	"(", operator_assignment, ")" |
	expression, binary_operator, { attribute_instance }, expression |
	conditional_expression |
	inside_expression |
	tagged_union_expression
	;

constant_indexed_range =
	constant_expression, "+:", constant_expression |
	constant_expression, "-:", constant_expression
	;

constant_range =
	constant_expression, ":", constant_expression
	;

constant_part_select_range =
	constant_range |
	constant_indexed_range
	;

constant_range_expression =
	constant_expression |
	constant_part_select_range
	;

param_expression =
	mintypmax_expression |
	data_type
	;

constant_param_expression =
	constant_mintypmax_expression |
	data_type |
	"$"
	;

constant_mintypmax_expression =
	constant_expression |
	constant_expression, ":", constant_expression, ":", constant_expression
	;

constant_expression =
	constant_primary |
	unary_operator, { attribute_instance }, constant_primary |
	constant_expression, binary_operator, { attribute_instance }, constant_expression |
	constant_expression, "?", { attribute_instance }, constant_expression, ":", constant_expression
	;

conditional_expression =
	cond_predicate, "?", { attribute_instance }, expression, ":", expression
	;

inc_or_dec_expression =
	inc_or_dec_operator, { attribute_instance }, variable_lvalue |
	variable_lvalue, { attribute_instance }, inc_or_dec_operator
	;

array_method_name =
	method_identifier |
	"unique" |
	"and" |
	"or" |
	"xor"
	;

method_call_root =
	expression |
	implicit_class_handle
	;

randomize_call =
	"randomize", { attribute_instance }, [ "(", [ variable_identifier_list | "null" ], ")" ],
		[ "with", constraint_block ]
	;

array_manipulation_call =
	array_method_name, { attribute_instance }, [ "(", list_of_arguments, ")" ],
		[ "with", "(", expression, ")" ]
	;

built_in_method_call =
	array_manipulation_call |
	randomize_call
	;

method_call_body =
	method_identifier, { attribute_instance }, [ "(", list_of_arguments, ")" ] |
	built_in_method_call
	;

method_call =
	method_call_root, ".", method_call_body
	;

list_of_arguments =
	[ expression ], { ",", [ expression ] }, { ",", ".", identifier, "(", [ expression ], ")" } |
	".", identifier, "(", [ expression ], ")", { ",", ".", identifier, "(", [ expression ], ")" }
	;

function_subroutine_call =
	subroutine_call
	;

subroutine_call =
	tf_call |
	system_tf_call |
	method_call |
	randomize_call
	;

system_tf_call =
	system_tf_identifier, [ "(", list_of_arguments, ")" ] |
	system_tf_identifier, "(", data_type, [ ",", expression ], ")"
	;

(* See NOTE 36 *)
tf_call =
	ps_or_hierarchical_tf_identifier, { attribute_instance }, [ "(", list_of_arguments, ")" ]
	;

constant_function_call =
	(* See NOTE 24 *)
	function_subroutine_call
	;

(* See NOTE 21 *)
empty_queue =
	"{", "}"
	;

array_range_expression =
	expression |
	expression, ":", expression |
	expression, "+:", expression |
	expression, "-:", expression
	;

stream_expression =
	expression, [ "with", "[", array_range_expression, "]" ]
	;

stream_concatenation =
	"{", stream_expression, { ",", stream_expression }, "}"
	;

slice_size =
	simple_type |
	constant_expression
	;

stream_operator =
	">>" | "<<"
	;

streaming_concatenation =
	"{",  stream_operator, [ slice_size ], stream_concatenation, "}"
	;

(* See NOTE 18 *)
multiple_concatenation =
	"{", expression, concatenation, "}"
	;

module_path_multiple_concatenation =
	"{", constant_expression, module_path_concatenation, "}"
	;

module_path_concatenation =
	"{", module_path_expression, { ",", module_path_expression }, "}"
	;

constant_multiple_concatenation =
	"{", constant_expression, constant_concatenation, "}"
	;

constant_concatenation =
	"{", constant_expression, { ",", constant_expression }, "}"
	;

concatenation =
	"{", expression, { ",", expression }, "}"
	;

scalar_constant =
	"1'b0" | "1'b1" | "1'B0" | "1'B1" | "'b0" | "'b1" | "'B0" | "'B1" | "1" | "0"
	;

scalar_timing_check_condition =
	expression |
	"~", expression |
	expression, "==", scalar_constant |
	expression, "!=", scalar_constant |
	expression, "===", scalar_constant |
	expression, "!==", scalar_constant
	;

timing_check_condition =
	scalar_timing_check_condition |
	"(", scalar_timing_check_condition, ")"
	;

z_or_x =
	"x" | "X" | "z" | "Z"
	;

zero_or_one =
	"0" | "1"
	;

(* See NOTE 1 *)
edge_descriptor =
	"01" |
	"10" |
	z_or_x, zero_or_one |
	zero_or_one, z_or_x
	;

edge_control_specifier =
	"edge", "[", edge_descriptor, { ",", edge_descriptor }, "]"
	;

specify_terminal_descriptor =
	specify_input_terminal_descriptor |
	specify_output_terminal_descriptor
	;

timing_check_event_control =
	"posedge" |
	"negedge" |
	edge_control_specifier
	;

controlled_timing_check_event =
	timing_check_event_control, specify_terminal_descriptor, [ "&&&", timing_check_condition ]
	;

timing_check_event =
	[ timing_check_event_control ], specify_terminal_descriptor, [ "&&&", timing_check_condition ]
	;

timing_check_limit =
	expression
	;

threshold =
	constant_expression
	;

start_edge_offset =
	mintypmax_expression
	;

stamptime_condition =
	mintypmax_expression
	;

remain_active_flag =
	constant_mintypmax_expression
	;

reference_event =
	timing_check_event
	;

notifier =
	variable_identifier
	;

event_based_flag =
	constant_expression
	;

end_edge_offset =
	mintypmax_expression
	;

delayed_reference =
	terminal_identifier |
	terminal_identifier, "[", constant_mintypmax_expression, "]"
	;

delayed_data =
	terminal_identifier |
	terminal_identifier, "[", constant_mintypmax_expression, "]"
	;

data_event =
	timing_check_event
	;

controlled_reference_event =
	controlled_timing_check_event
	;

checktime_condition =
	mintypmax_expression
	;

nochange_timing_check =
	"$nochange", "(", reference_event, ",", data_event, ",", start_edge_offset, ",", end_edge_offset,
		[ ",", [ notifier ] ] ,")", ";"
	;

width_timing_check =
	"$width", "(", controlled_reference_event, ",", timing_check_limit, ",", threshold, [ ",", [ notifier ] ],
		")", ";"
	;

period_timing_check =
	"$period", "(", controlled_reference_event, ",", timing_check_limit, [ ",", [ notifier ]  ], ")", ";"
	;

fullskew_timing_check =
	"$fullskew", "(", reference_event, ",", data_event, ",", timing_check_limit, ",", timing_check_limit,
		[ ",", [ notifier ], [ ",", [ event_based_flag ], [ ",", [ remain_active_flag ] ] ] ], ")", ";"
	;

timeskew_timing_check =
	"$timeskew", "(", reference_event, ",", data_event, ",", timing_check_limit, [ ",", [ notifier ],
		[ ",", [ event_based_flag ], [ ",", [ remain_active_flag ] ] ] ], ")", ";"
	;

skew_timing_check =
	"$skew", "(", reference_event, ",", data_event, ",", timing_check_limit, [ ",", [ notifier ] ], ")", ";"
	;

recrem_timing_check =
	"$recrem", "(",  reference_event, ",", data_event, ",", timing_check_limit, ",", timing_check_limit,
		[ ",", [ notifier ], [ ",", [ stamptime_condition ], [ ",", [ checktime_condition,
			[ ",", [ delayed_reference ], [ ",", [ delayed_data ] ] ] ] ] ] ], ")", ";"
	;

removal_timing_check =
	"$removal", "(", reference_event, ",", data_event, ",", timing_check_limit, [ ",", [ notifier ] ], ")", ";"
	;

recovery_timing_check =
	"$recover", "(", reference_event, ",", data_event, ",", timing_check_limit, [ ",", [ notifier ] ], ")", ";"
	;

setuphold_timing_check =
	"$setuphold", "(", reference_event, ",", data_event, ",", timing_check_limit, ",", timing_check_limit,
		[ ",", [ notifier ], [ ",", [ stamptime_condition ], [ ",", [ checktime_condition,
			[ ",", [ delayed_reference ], [ ",", [ delayed_data ] ] ] ] ] ] ], ")", ";"
	;

hold_timing_check =
	"$hold", "(", reference_event, ",", data_event, ",", timing_check_limit, [ ",", [ notifier ] ], ")", ";"
	;

setup_timing_check =
	"$setup", "(", data_event, ",", reference_event, ",", timing_check_limit, [ ",", [ notifier ] ], ")", ";"
	;

system_timing_check =
	setup_timing_check |
	hold_timing_check |
	setuphold_timing_check |
	recovery_timing_check |
	removal_timing_check |
	recrem_timing_check |
	skew_timing_check |
	timeskew_timing_check |
	fullskew_timing_check |
	period_timing_check |
	width_timing_check |
	nochange_timing_check
	;

polarity_operator =
	"+" | "-"
	;

state_dependent_path_declaration =
	"if", "(", module_path_expression, ")", simple_path_declaration |
	"if", "(", module_path_expression, ")", edge_sensitive_path_declaration |
	"ifnone", simple_path_declaration
	;

edge_identifier =
	"posedge" | "negedge"
	;

data_source_expression =
	expression
	;

full_edge_sensitive_path_description =
	"(", [ edge_identifier ], list_of_path_inputs, "*>",
		"(", list_of_path_outputs, [ polarity_operator ], ":", data_source_expression, ")", ")"
	;

parallel_edge_sensitive_path_description =
	"(", [ edge_identifier ], specify_input_terminal_descriptor, "=>",
		"(", specify_output_terminal_descriptor, [ polarity_operator ], ":", data_source_expression, ")", ")"
	;

edge_sensitive_path_declaration =
	parallel_edge_sensitive_path_description, "=", path_delay_value |
	full_edge_sensitive_path_description, "=", path_delay_value
	;

path_delay_expression =
	constant_mintypmax_expression
	;

tzx_path_delay_expression =
	path_delay_expression
	;

txz_path_delay_expression =
	path_delay_expression
	;

tx0_path_delay_expression =
	path_delay_expression
	;

t1x_path_delay_expression =
	path_delay_expression
	;

tx1_path_delay_expression =
	path_delay_expression
	;

t0x_path_delay_expression =
	path_delay_expression
	;

tz0_path_delay_expression =
	path_delay_expression
	;

t1z_path_delay_expression =
	path_delay_expression
	;

tz1_path_delay_expression =
	path_delay_expression
	;

t0z_path_delay_expression =
	path_delay_expression
	;

t10_path_delay_expression =
	path_delay_expression
	;

t01_path_delay_expression =
	path_delay_expression
	;

tz_path_delay_expression =
	path_delay_expression
	;

tfall_path_delay_expression =
	path_delay_expression
	;

trise_path_delay_expression =
	path_delay_expression
	;

t_path_delay_expression =
	path_delay_expression
	;

list_of_path_delay_expressions =
	t_path_delay_expression |
	trise_path_delay_expression, ",", tfall_path_delay_expression |
	trise_path_delay_expression, ",", tfall_path_delay_expression, ",", tz_path_delay_expression, "," |
	t01_path_delay_expression, ",", t10_path_delay_expression, ",", t0z_path_delay_expression, ",",
	tz1_path_delay_expression, ",", t1z_path_delay_expression, ",", tz0_path_delay_expression |
	t01_path_delay_expression, ",", t10_path_delay_expression, ",", t0z_path_delay_expression, ",",
	tz1_path_delay_expression, ",", t1z_path_delay_expression, ",", tz0_path_delay_expression, ",",
	t0x_path_delay_expression, ",", tx1_path_delay_expression, ",", t1x_path_delay_expression, ",",
	tx0_path_delay_expression, ",", txz_path_delay_expression, ",", tzx_path_delay_expression
	;

path_delay_value =
	list_of_path_delay_expressions, |
	"(", list_of_path_delay_expressions, ")"
	;

output_identifier =
	output_port_identifier |
	inout_port_identifier |
	interface_identifier, ".", port_identifier
	;

input_identifier =
	input_port_identifier |
	inout_port_identifier |
	interface_identifier, ".", port_identifier
	;

specify_output_terminal_descriptor =
	output_identifier, [ "[", constant_range_expression, "]" ]
	;

specify_input_terminal_descriptor =
	input_identifier, [ "[", constant_range_expression, "]" ]
	;

list_of_path_outputs =
	specify_output_terminal_descriptor, { ",", specify_output_terminal_descriptor }
	;

list_of_path_inputs =
	specify_input_terminal_descriptor, { ",", specify_input_terminal_descriptor }
	;

full_path_description =
	"(", list_of_path_inputs, [ polarity_operator ], "*>", list_of_path_outputs, ")"
	;

parallel_path_description =
	"(", specify_input_terminal_descriptor, [ polarity_operator ], "=>", specify_output_terminal_descriptor, ")"
	;

simple_path_declaration =
	parallel_path_description, "=", path_delay_value |
	full_path_description, "=", path_delay_value
	;

path_declaration =
	simple_path_declaration, ";" |
	edge_sensitive_path_declaration, ";" |
	state_dependent_path_declaration, ";"
	;

showcancelled_declaration =
	"showcancelled", list_of_path_outputs, ";" |
	"noshowcancelled", list_of_path_outputs, ";" |
	;

pulsestyle_declaration =
	"pulsestyle_onevent", list_of_path_outputs, ";" |
	"pulsestyle_ondetect", list_of_path_outputs, ";"
	;

specify_item =
	specparam_declaration |
	pulsestyle_declaration |
	showcancelled_declaration |
	path_declaration |
	system_timing_check |
	;

specify_block =
	"specify", { specify_item }, "endspecify"
	;

rs_case_item =
	expression, { ",", expression }, ":", production_item, ";" |
	"default", [ ":" ], production_item, ";"
	;

rs_case =
	"case", "(", expression, ")", rs_case_item, { rs_case_item }, "endcase"
	;

rs_repeat =
	"repeat", "(", expression, ")", production_item
	;

rs_if_else =
	"if", "(", expression, ")", production_item, [ "else", production_item ]
	;

production_item =
	production_identifier, [ "(", list_of_arguments, ")" ]
	;

rs_prod =
	production_item |
	rs_code_block |
	rs_if_else |
	rs_repeat |
	rs_case
	;

rs_code_block =
	"{", { data_declaration }, { statement_or_null }, "}"
	;

weight_specification =
	integral_number |
	ps_identifier |
	"(", expression, ")"
	;

rs_production_list =
	rs_prod, { rs_prod } |
	"rand", "join", [ "(", expression, ")" ], production_item, production_item, { production_item }
	;

rs_rule =
	rs_production_list, [ ":=", weight_specification, [ rs_code_block ] ]
	;

production =
	[ function_data_type ], production_identifier, [ "(", tf_port_list, ")" ], ":", rs_rule,
		{ "|", rs_rule }, ";"
	;

randsequence_statement =
	"randsequence", "(", [ production_identifier ], ")", production, { production }, "endsequence"
	;

clockvar_expression =
	clockvar, select
	;

clockvar =
	hierarchical_identifier
	;

cycle_delay =
	"##", integral_number |
	"##", identifier |
	"##", "(", expression, ")"
	;

clocking_drive =
	clockvar_expression, "<=", [ cycle_delay ], expression |
	cycle_delay, clockvar_expression, "<=", expression
	;

clocking_skew =
	edge_identifier, [ delay_control ] |
	delay_control
	;

clocking_decl_assign =
	signal_identifier, [ "=", expression ]
	;

list_of_clocking_decl_assign =
	clocking_decl_assign, { ",", clocking_decl_assign }
	;

clocking_direction =
	"input", [ clocking_skew ] |
	"output", [ clocking_skew ] |
	"input", [ clocking_skew ], "output", [ clocking_skew ] |
	"inout"
	;

default_skew =
	"input", clocking_skew |
	"output", clocking_skew |
	"input", clocking_skew, "output", clocking_skew
	;

clocking_item =
	"default", default_skew, ";" |
	clocking_direction, list_of_clocking_decl_assign, ";" |
	{ attribute_instance }, concurrent_assertion_item_declaration
	;

clocking_event =
	"@", identifier |
	"@", "(", event_expression, ")"
	;

clocking_declaration =
	[ "default" ], "clocking", [ clocking_identifier ], clocking_event, ";", { clocking_item },
		"endclocking", [ ":", clocking_identifier ]
	;

immediate_assert_statement =
	"assert", "(", expression, ")", action_block
	;

procedural_assertion_statement =
	concurrent_assertion_statement |
	immediate_assert_statement
	;

subroutine_call_statement =
	subroutine_call, ";" |
	"void", "'", "(", function_subroutine_call, ")", ";"
	;

loop_variables =
	[ index_variable_identifier ], { ",", [ index_variable_identifier ] }
	;

for_step_assignment =
	operator_assignment |
	inc_or_dec_expression |
	function_subroutine_call
	;

for_step =
	for_step_assignment, { ",", for_step_assignment }
	;

for_variable_declaration =
	data_type, variable_identifier, "=", expression, { ",", variable_identifier, "=", expression }
	;

for_initialization =
	list_of_variable_assignments |
	for_variable_declaration, { ",", for_variable_declaration }
	;

loop_statement =
	"forever", statement_or_null |
	"repeat", "(", expression, ")", statement_or_null |
	"while", "(", expression, ")", statement_or_null |
	"for", "(", for_initialization, ";", expression, ";", for_step, ")", statement_or_null |
	"do", statement_or_null, "while", "(", expression, ")", ";" |
	"foreach", "(", array_identifier, "[", loop_variables, "]", ")", statement
	;

assignment_pattern_variable_lvalue =
	"'", "{",  variable_lvalue, { ",", variable_lvalue }, "}"
	;

assignment_pattern_net_lvalue =
	"'", "{",  net_lvalue, { ",", net_lvalue }, "}"
	;

(* See NOTE 35 *)
constant_assignment_pattern_expression =
	assignment_pattern_expression
	;

assignment_pattern_expression_type =
	ps_type_identifier |
	ps_parameter_identifier |
	integer_atom_type
	;

assignment_pattern_expression =
	[ assignment_pattern_expression_type ], assignment_pattern
	;

assignment_pattern_key =
	simple_type |
	"default"
	;

array_pattern_key =
	constant_expression |
	assignment_pattern_key
	;

structure_pattern_key =
	member_identifier |
	assignment_pattern_key
	;

assignment_pattern =
	"'", "{", expression, { ",", expression }, "}" |
	"'", "{", structure_pattern_key, ":", expression, { ",", structure_pattern_key, ":", expression }, "}" |
	"'", "{", array_pattern_key, ":", expression, { ",", array_pattern_key, ":", expression }, "}" |
	"'", "{", constant_expression, "{", expression, { ",", expression }, "}", "}"
	;

pattern =
	".", variable_identifier |
	".*" |
	constant_expression |
	"tagged", member_identifier, [ pattern ] |
	"'", "{", pattern, { ",", pattern }, "}" |
	"'", "{", member_identifier, ":", pattern, { ",", member_identifier, ":", pattern }, "}"
	;

randcase_item =
	expression, ":", statement_or_null
	;

randcase_statement =
	"randcase", randcase_item, { randcase_item }, "endcase"
	;

case_inside_item =
	open_range_list, ":", statement_or_null |
	"default", [ ":" ], statement_or_null
	;

case_pattern_item =
	pattern, [ "&&&", expression ], ":", statement_or_null |
	"default", [ ":" ], statement_or_null
	;

case_item =
	expression, { ",", expression }, ":", statement_or_null |
	"default", [ ":" ], statement_or_null
	;

case_keyword =
	"case" | "casez" | "casex"
	;

case_statement =
	[ unique_priority ], case_keyword, "(", expression, ")", case_item, { case_item }, "endcase" |
	[ unique_priority ], case_keyword, "(", expression, ")", "matches",
		case_pattern_item, { case_pattern_item }, "endcase" |
	[ unique_priority ], case_keyword, "(", expression, ")", "inside",
		case_inside_item, { case_inside_item }, "endcase"
	;

cond_pattern =
	expression, "matches", pattern
	;

expression_or_cond_pattern =
	expression |
	cond_pattern
	;

cond_predicate =
	expression_or_cond_pattern, { "&&&", expression_or_cond_pattern }
	;

unique_priority =
	"unique" | "priority"
	;

unique_priority_if_statement =
	[ unique_priority ], "if", "(", cond_predicate, ")", statement_or_null,
		{ "else", "if", "(", cond_predicate, ")", statement_or_null  }, [ "else", statement_or_null ]
	;

conditional_statement =
	"if", "(", cond_predicate, ")", statement_or_null, [ "else", statement_or_null ] |
	unique_priority_if_statement
	;

disable_statement =
	"disable", hierarchical_task_identifier, ";" |
	"disable", hierarchical_block_identifier, ";" |
	"disable", "fork", ";"
	;

event_trigger =
	"->", hierarchical_event_identifier, ";" |
	"->>", [ delay_or_event_control ], hierarchical_event_identifier, ";"
	;

wait_statement =
	"wait", "(", expression, ")", statement_or_null |
	"wait", "fork", ";" |
	"wait_order", "(", hierarchical_identifier, { ",", hierarchical_identifier }, ")", action_block
	;

jump_statement =
	"return", [ expression ], ";" |
	"break", ";" |
	"continue", ";"
	;

procedural_timing_control =
	delay_control |
	event_control |
	cycle_delay
	;

event_expression =
	[ edge_identifier ], expression, [ "iff", expression ] |
	sequence_instance, [ "iff", expression ] |
	event_expression, "or", event_expression |
	event_expression, ",", event_expression
	;

event_control =
	"@", hierarchical_event_identifier |
	"@", "(", event_expression, ")" |
	"@", "*" |
	"@", "(", "*", ")" |
	"@", sequence_instance
	;

delay_control =
	"#", delay_value |
	"#", "(", mintypmax_expression, ")"
	;

delay_or_event_control =
	delay_control |
	event_control |
	"repeat", "(", expression, ")", event_control
	;

procedural_timing_control_statement =
	procedural_timing_control, statement_or_null
	;

variable_identifier_list =
	variable_identifier, { ",", variable_identifier }
	;

function_statement_or_null =
	function_statement |
	{ attribute_instance }, ";"
	;

function_statement =
	statement
	;

statement_item =
	blocking_assignment, ";" |
	nonblocking_assignment, ";" |
	procedural_continuous_assignments, ";" |
	case_statement |
	conditional_statement |
	inc_or_dec_expression, ";" |
	subroutine_call_statement |
	disable_statement |
	event_trigger |
	loop_statement |
	jump_statement |
	par_block |
	procedural_timing_control_statement |
	seq_block |
	wait_statement |
	procedural_assertion_statement |
	clocking_drive, ";" |
	randsequence_statement |
	randcase_statement |
	expect_property_statement
	;

statement =
	[ block_identifier, ":" ], { attribute_instance }, statement_item
	;

statement_or_null =
	statement |
	{ attribute_instance }, ";"
	;

join_keyword =
	"join" | "join_any" | "join_none"
	;

par_block =
	"fork", [ ":", block_identifier ], { block_item_declaration }, { statement_or_null },
		join_keyword, [ ":", block_identifier ]
	;

seq_block =
	"begin", [ ":", block_identifier ], { block_item_declaration }, { statement_or_null },
		"end", [ ":", block_identifier ]
	;

action_block =
	statement_or_null |
	[ statement ], "else", statement_or_null
	;

variable_assignment =
	variable_lvalue, "=", expression
	;

procedural_continuous_assignments =
	"assign", variable_assignment |
	"deassign", variable_lvalue |
	"force", variable_assignment |
	"force", net_assignment |
	"release", variable_lvalue |
	"release", net_lvalue
	;

nonblocking_assignment =
	variable_lvalue, "<=", [ delay_or_event_control ], expression
	;

assignment_operator =
	"=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" | ">>=" | "<<<=" | ">>>="
	;

operator_assignment =
	variable_lvalue, assignment_operator, expression
	;

blocking_assignment =
	variable_lvalue, "=", delay_or_event_control, expression |
	hierarchical_dynamic_array_variable_identifier, "=", dynamic_array_new |
	[ implicit_class_handle, "." | class_scope | package_scope ], hierarchical_variable_identifier,
		select, "=", class_new |
	operator_assignment
	;

final_construct =
	"final", function_statement
	;

always_keyword =
	"always" | "always_comb" | "always_latch" | "always_ff"
	;

always_construct =
	always_keyword, statement
	;

initial_construct =
	"initial", statement_or_null
	;

net_assignment =
	net_lvalue, "=", expression
	;

net_alias =
	"alias", net_lvalue, "=", net_lvalue, { "=", net_lvalue }, ";"
	;

list_of_variable_assignments =
	variable_assignment, { ",", variable_assignment }
	;

list_of_net_assignments =
	net_assignment, { ",", net_assignment }
	;

continuous_assign =
	"assign", [ drive_strength ], [ delay3 ], list_of_net_assignments, ";" |
	"assign", [ delay_control ], list_of_variable_assignments, ";"
	;

udp_instance =
	[ name_of_instance ], "(", output_terminal, ",", input_terminal, { ",", input_terminal }, ")"
	;

udp_instantiation =
	udp_identifier, [ drive_strength ], [ delay2 ], udp_instance, { ",", udp_instance }, ";"
	;

edge_symbol =
	"r" | "R" | "f" | "F" | "p" | "P" | "n" | "N" | "*"
	;

level_symbol =
	"0"| "1" | "x" | "X" | "?" | "b" | "B"
	;

output_symbol =
	"0" | "1" | "x" | "X"
	;

next_state =
	output_symbol | "-"
	;

current_state =
	level_symbol
	;

edge_indicator =
	"(", level_symbol, level_symbol, ")" |
	edge_symbol
	;

edge_input_list =
	{ level_symbol }, edge_indicator, { level_symbol }
	;

level_input_list =
	level_symbol, { level_symbol }
	;

seq_input_list =
	level_input_list |
	edge_input_list
	;

sequential_entry =
	seq_input_list, ":", current_state, ":", next_state, ";"
	;

init_val =
	"1'b0" | "1'b1" | "1'bx" | "1'bX" | "1'B0" | "1'B1" | "1'Bx" | "1'BX" | "1" | "0"
	;

udp_initial_statement =
	"initial", output_port_identifier, "=", init_val, ";"
	;

sequential_body =
	[ udp_initial_statement ], "table", sequential_entry, { sequential_entry }, "endtable"
	;

combinational_entry =
	level_input_list, ":", output_symbol, ";"
	;

combinational_body =
	"table", combinational_entry, { combinational_entry }, "endtable"
	;

udp_body =
	combinational_body |
	sequential_body
	;

udp_reg_declaration =
	{ attribute_instance }, "reg", variable_identifier
	;

udp_input_declaration =
	{ attribute_instance }, "input", list_of_udp_port_identifiers
	;

udp_output_declaration =
	{ attribute_instance }, "output", port_identifier |
	{ attribute_instance }, "output", "reg", port_identifier, [ "=", constant_expression ]
	;

udp_port_declaration =
	udp_output_declaration, ";" |
	udp_input_declaration, ";" |
	udp_reg_declaration, ";"
	;

udp_declaration_port_list =
	udp_output_declaration, ",", udp_input_declaration, { ",", udp_input_declaration }
	;

udp_port_list =
	output_port_identifier, ",", input_identifier, { ",", input_port_identifier }
	;

udp_declaration =
	udp_nonansi_declaration, udp_port_declaration, { udp_port_declaration }, udp_body, "endprimitive",
		[ ":", udp_identifier ] |
	udp_ansi_declaration, udp_body, "endprimitive", [ ":", udp_identifier ] |
	"extern", udp_nonansi_declaration |
	"extern", udp_ansi_declaration |
	{ attribute_instance }, "primitive", udp_identifier, "(", ".*", ")", ";", { udp_port_declaration },
		udp_body, "endprimitive", [ ":", udp_identifier ]
	;

udp_ansi_declaration =
	{ attribute_instance }, "primitive", udp_identifier, "(", udp_declaration_port_list, ")", ";"
	;

udp_nonansi_declaration =
	{ attribute_instance }, "primitive", udp_identifier, "(", udp_port_list, ")", ";"
	;

generate_block_or_null =
	generate_block |
	";"
	;

generate_block =
	module_or_interface_or_generate_item |
	[ generate_block_identifier, ":" ], "begin", [ ":", generate_block_identifier ],
		{ module_or_interface_or_generate_item }, "end", [ ":", generate_block_identifier ]
	;

case_generate_item =
	constant_expression, { ",", constant_expression }, ":", generate_block_or_null |
	"default", [ ":" ], generate_block_or_null
	;

case_generate_construct =
	"case", "(", constant_expression, ")", case_generate_item, { case_generate_item }, "endcase"
	;

if_generate_construct =
	"if", "(", constant_expression, ")", generate_block_or_null, [ "else", generate_block_or_null ]
	;

conditional_generate_construct =
	if_generate_construct |
	case_generate_construct
	;

genvar_iteration =
	genvar_identifier, assignment_operator, genvar_expression |
	inc_or_dec_operator, genvar_identifier |
	genvar_identifier, inc_or_dec_operator
	;

genvar_initialization =
	[ "genvar" ], genvar_identifier, "=", constant_expression
	;

loop_generate_construct =
	"for", "(", genvar_initialization, ";", genvar_expression, ";", genvar_iteration, ")", generate_block
	;

generate_region =
	"generate", { module_or_interface_or_generate_item }, "endgenerate"
	;

(* See NOTE 31 *)
module_or_interface_or_generate_item =
	module_or_generate_item |
	interface_or_generate_item
	;

program_instantiation =
	program_identifier, [ parameter_value_assignment ], hierarchical_instance, { ",", hierarchical_instance }, ";"
	;

interface_instantiation =
	interface_identifier, [ parameter_value_assignment ], hierarchical_instance, { ",", hierarchical_instance }, ";"
	;

named_port_connection =
	{ attribute_instance }, ".", port_identifier, [ "(", [ expression ], ")" ] |
	{ attribute_instance }, ".*"
	;

ordered_port_connection =
	{ attribute_instance }, [ expression ]
	;

(* See NOTE 16 *)
list_of_port_connections =
	ordered_port_connection, { ",", ordered_port_connection } |
	named_port_connection, { ",", named_port_connection }
	;

name_of_instance =
	instance_identifier, { unpacked_dimension }
	;

hierarchical_instance =
	name_of_instance, "(", [ list_of_port_connections ] , ")"
	;

named_parameter_assignment =
	".", parameter_identifier, "(", [ param_expression ], ")"
	;

ordered_parameter_assignment =
	param_expression
	;

list_of_parameter_assignments =
	ordered_parameter_assignment, { ",", ordered_parameter_assignment } |
	named_parameter_assignment, { ",", named_parameter_assignment }
	;

parameter_value_assignment =
	"#", "(", [ list_of_parameter_assignments ], ")"
	;

module_instantiation =
	module_identifier, [ parameter_value_assignment ], hierarchical_instance, { ",", hierarchical_instance }, ";"
	;

pass_switchtype =
	"tran" | "rtran"
	;

pass_en_switchtype =
	"tranif0" | "tranif1" | "rtranif1" | "rtranif0"
	;

n_output_gatetype =
	"buf" | "not"
	;

n_input_gatetype =
	"and" | "nand" | "or" | "nor" | "xor" | "xnor"
	;

mos_switchtype =
	"nmos" | "pmos" | "rnmos" | "rpmos"
	;

enable_gatetype =
	"bufif0" | "bufif1" | "notif0" | "notif1"
	;

cmos_switchtype =
	"cmos" | "rcmos"
	;

pcontrol_terminal =
	expression
	;

output_terminal =
	net_lvalue
	;

ncontrol_terminal =
	expression
	;

input_terminal =
	expression
	;

inout_terminal =
	net_lvalue
	;

enable_terminal =
	expression
	;

pullup_strength =
	"(", strength0, strength1, ")" |
	"(", strength1, strength0, ")" |
	"(", strength1, ")"
	;

pulldown_strength =
	"(", strength0, strength1, ")" |
	"(", strength1, strength0, ")" |
	"(", strength0, ")"
	;

pull_gate_instance =
	[ name_of_instance ], "(", output_terminal, ")"
	;

pass_enable_switch_instance =
	[ name_of_instance ], "(", inout_terminal, ",", inout_terminal, ",", enable_terminal, ")"
	;

pass_switch_instance =
	[ name_of_instance ], "(", inout_terminal, ",", inout_terminal, ")"
	;

n_output_gate_instance =
	[ name_of_instance ], "(", output_terminal, { ",", output_terminal }, ",", input_terminal, ")"
	;

n_input_gate_instance =
	[ name_of_instance ], "(", output_terminal, ",", input_terminal, { ",", input_terminal }, ")"
	;

mos_switch_instance =
	[ name_of_instance ], "(", output_terminal, ",", input_terminal, ",", enable_terminal, ")"
	;

enable_gate_instance =
	[ name_of_instance ], "(", output_terminal, ",", input_terminal, ",", enable_terminal, ")"
	;

cmos_switch_instance =
	[ name_of_instance ], "(", output_terminal, ",", input_terminal, ",", ncontrol_terminal, ",", pcontrol_terminal, ")"
	;

gate_instantiation =
	cmos_switchtype, [ delay3 ], cmos_switch_instance, { ",", cmos_switch_instance }, ";"|
	enable_gatetype, [ drive_strength ], [ delay3 ], enable_gate_instance, { ",", enable_gate_instance }, ";" |
	mos_switchtype, [ delay3 ], mos_switch_instance, { ",", mos_switch_instance }, ";" |
	n_input_gatetype, [ drive_strength ], [ delay2 ], n_input_gate_instance, { ",", n_input_gate_instance }, ";" |
	n_output_gatetype, [ drive_strength ], [ delay2 ], n_output_gate_instance, { ",", n_output_gate_instance }, ";" |
	pass_en_switchtype, [ delay2 ], pass_enable_switch_instance, { ",", pass_enable_switch_instance }, ";" |
	pass_switchtype, pass_switch_instance, { ",", pass_switch_instance }, ";" |
	"pulldown", [ pulldown_strength ], pull_gate_instance, { ",", pull_gate_instance }, ";" |
	"pullup", [ pullup_strength ], pull_gate_instance, { ",", pull_gate_instance }, ";"
	;

(* See NOTE 20 *)
open_value_range =
	value_range
	;

open_range_list =
	open_value_range, { ",", open_value_range }
	;

bins_expression =
	variable_identifier |
	cover_point_identifier, [ ".", bins_identifier ]
	;

select_condition =
	"binsof", "(", bins_expression, ")", [ "intersect", "{", open_range_list, "}" ]
	;

select_expression =
	select_condition |
	"!", select_condition |
	select_expression, "&&", select_expression |
	select_expression, "||", select_expression |
	"(", select_expression, ")"
	;

bins_selection =
	bins_keyword, bin_identifier, "=", select_expression, [ "iff", "(", expression, ")" ]
	;

bins_selection_or_option =
	{ attribute_instance }, coverage_option |
	{ attribute_instance }, bins_selection
	;

select_bins_or_empty =
	"{", { bins_selection_or_option, ";" }, "}" |
	";"
	;

cross_item =
	cover_point_identifier |
	variable_identifier
	;

list_of_coverpoints =
	cross_item, ",", cross_item, { ",", cross_item }
	;

cover_cross =
	[ cover_point_identifier, ":" ], "cross", list_of_coverpoints, [ "iff", "(", expression, ")" ],
		select_bins_or_empty
	;

repeat_range =
	expression |
	expression, ":", expression
	;

trans_item =
	range_list
	;

trans_range_list =
	trans_item |
	trans_item, [ "[", "*", repeat_range, "]" ] |
	trans_item, [ "[", "->", repeat_range, "]" ] |
	trans_item, [ "[", "=", repeat_range, "]" ]
	;

trans_set =
	trans_range_list, { "=>", trans_range_list }
	;

trans_list =
	"(", trans_set, ")", { ",", "(", trans_set, ")" }
	;

range_list =
	value_range, { ",", value_range }
	;

bins_keyword =
	"bins" | "illegal_bins" | "ignore_bins"
	;

bins_or_options =
	coverage_option |
	[ "wildcard" ], bins_keyword, bin_identifier, [ "[", [ expression ], "]" ], "=", "{", open_value_range, "}",
		[ "iff", "(", expression, ")" ] |
	[ "wildcard" ], bins_keyword, bin_identifier, [ "[", "]" ], "=", trans_list, [ "iff", "(", expression, ")" ] |
	bins_keyword, bin_identifier, [ "[", [ expression ], "]" ], "=", "default", [ "iff", "(", expression, ")" ] |
	bins_keyword, bin_identifier, "=", "default", "sequence", [ "iff", "(", expression, ")" ]
	;

bins_or_empty =
	"{", { attribute_instance }, { bins_or_options, ";" }, "}" |
	";"
	;

cover_point =
	[ cover_point_identifier, ":" ], "coverpoint", expression, [ "iff", "(", expression, ")" ], bins_or_empty
	;

hierarchical_btf_identifier =
	hierarchical_tf_identifier |
	hierarchical_block_identifier |
	hierarchical_identifier, [ class_scope ], method_identifier
	;

block_event_expression =
	block_event_expression, "or", block_event_expression |
	"begin", hierarchical_btf_identifier |
	"end", hierarchical_btf_identifier
	;

coverage_event =
	clocking_event |
	"@", "@", "(", block_event_expression, ")"
	;

coverage_spec =
	cover_point |
	cover_cross
	;

coverage_option =
	"option", ".", member_identifier, "=", expression |
	"type_option", ".", member_identifier, "=", expression
	;

coverage_spec_or_option =
	{ attribute_instance }, coverage_spec |
	{ attribute_instance }, coverage_option, ";"
	;

covergroup_declaration =
	"covergroup", covergroup_identifier, [ "(", [ tf_port_list ], ")" ], [ coverage_event ], ";",
		{ coverage_spec_or_option }, "endgroup", [ ":", covergroup_identifier ]
	;

assertion_variable_declaration =
	var_data_type, list_of_variable_decl_assignments, ";"
	;

expression_or_dist =
	expression, [ "dist", "{", dist_list, "}" ]
	;

cycle_delay_const_range_expression =
	constant_expression, ":", constant_expression |
	constant_expression, ":", "$"
	;

const_or_range_expression =
	constant_expression |
	cycle_delay_const_range_expression
	;

goto_repetition =
	"[", "->", const_or_range_expression, "]"
	;

non_consecutive_repetition =
	"[", "=", const_or_range_expression, "]"
	;

consecutive_repetition =
	"[", "*", const_or_range_expression, "]"
	;

sequence_abbrev =
	consecutive_repetition
	;

boolean_abbrev =
	consecutive_repetition |
	non_consecutive_repetition |
	goto_repetition
	;

actual_arg_expr =
	event_expression |
	"$"
	;

list_of_formals =
	formal_list_item, { ",", formal_list_item }
	;

formal_list_item =
	formal_identifier, [ "=", actual_arg_expr ]
	;

sequence_instance =
	ps_sequence_identifier, [ "(", [ list_of_arguments ], ")" ]
	;

sequence_match_item =
	operator_assignment |
	inc_or_dec_expression |
	subroutine_call
	;

sequence_method_call =
	sequence_instance, ".", method_identifier
	;

cycle_delay_range =
	"##", integral_number |
	"##", identifier |
	"##", "(", constant_expression, ")" |
	"##", "[", cycle_delay_const_range_expression, "]"
	;

sequence_expr =
	cycle_delay_range, sequence_expr, { cycle_delay_range, sequence_expr, } |
	sequence_expr, cycle_delay_range, sequence_expr, { cycle_delay_range, sequence_expr, } |
	expression_or_dist, [ boolean_abbrev ] |
	"(", expression_or_dist, { ",", sequence_match_item }, ")", [ boolean_abbrev ] |
	sequence_instance, [ sequence_abbrev ] |
	"(", sequence_expr, { ",", sequence_match_item, }, ")", [ sequence_abbrev ] |
	sequence_expr, "and", sequence_expr |
	sequence_expr, "intersect", sequence_expr |
	sequence_expr, "or", sequence_expr |
	"first_match", "(", sequence_expr, { ",", sequence_match_item }, ")" |
	expression_or_dist, "throughout", sequence_expr |
	sequence_expr, "within", sequence_expr |
	clocking_event, sequence_expr
	;

sequence_declaration =
	"sequence", sequence_identifier, [ "(", [ tf_port_list ], ")" ], ";", { assertion_variable_declaration },
		{ sequence_expr }, ";", "endsequence", [ ":", sequence_identifier ]
	;

property_expr =
	sequence_expr |
	"(", property_expr, ")" |
	"not", property_expr |
	property_expr, "or", property_expr |
	property_expr, "and", property_expr |
	sequence_expr, "|->", property_expr |
	"if", "(", expression_or_dist, ")", property_expr, [ "else", property_expr ] |
	property_instance |
	clocking_event, property_expr
	;

property_spec =
	[ clocking_event ], [ "disable", "iff", "(", expression_or_dist, ")" ], property_expr
	;

property_declaration =
	"property", property_identifier, [ "(", [ tf_port_list ], ")" ], ";", { assertion_variable_declaration },
		property_spec, ";", "endproperty", [ ":", property_identifier ]
	;

concurrent_assertion_item_declaration =
	property_declaration |
	sequence_declaration
	;

property_instance =
	ps_property_identifier, [ "(", [ list_of_arguments ], ")" ]
	;

expect_property_statement =
	"expect", "(", property_spec, ")", action_block
	;

cover_property_statement =
	"cover", "property", "(", property_spec, ")", statement_or_null
	;

assume_property_statement =
	"assume", "property", "(", property_spec, ")", ";"
	;

assert_property_statement =
	"assert", "property", "(", property_spec, ")", action_block
	;

concurrent_assertion_statement =
	assert_property_statement |
	assume_property_statement |
	cover_property_statement
	;

concurrent_assertion_item =
	[ block_identifier, ":" ], concurrent_assertion_statement
	;

import_export =
	"import" | "export"
	;

modport_tf_port =
	method_prototype |
	tf_identifier
	;

modport_tf_ports_declaration =
	import_export, modport_tf_port, { ",", modport_tf_port }
	;

modport_simple_port =
	port_identifier |
	".", port_identifier, "(", [ expression ], ")"
	;

modport_simple_ports_declaration =
	port_direction, modport_simple_port, { ",", modport_simple_port }
	;

modport_clocking_declaration =
	"clocking", clocking_identifier
	;

modport_ports_declaration =
	{ attribute_instance }, modport_simple_ports_declaration |
	{ attribute_instance }, modport_tf_ports_declaration |
	{ attribute_instance }, modport_clocking_declaration
	;

modport_item =
	modport_identifier, "(", modport_ports_declaration, { ",", modport_ports_declaration }, ")"
	;

modport_declaration =
	"modport", modport_item, { ",", modport_item }
	;

virtual_interface_declaration =
	"virtual", [ "interface" ], interface_identifier, list_of_virtual_interface_decl, ";"
	;

overload_proto_formals =
	data_type, { ",", data_type }
	;

overload_operator =
	"+" | "++" | "-" | "--" | "*" | "**" | "/" | "%" | "==" | "!=" | "<" | "<=" | ">" | ">=" | "="
	;

overload_declaration =
	"bind", overload_operator, "function", data_type, function_identifier, "(", overload_proto_formals, ")", ";"
	;

block_item_declaration =
	{ attribute_instance }, data_declaration |
	{ attribute_instance }, local_parameter_declaration |
	{ attribute_instance }, parameter_declaration, ";" |
	{ attribute_instance }, overload_declaration
	;

task_prototype =
	"task", task_identifier, "(", [ tf_port_list ], ")"
	;

tf_port_declaration =
	{ attribute_instance }, tf_port_declaration, [ "var" ], data_type_or_implicit,
		list_of_tf_variable_identifiers, ";"
	;

tf_port_direction =
	port_direction |
	"const", "ref"
	;

(* See NOTE 34 *)
tf_port_item =
	{ attribute_instance }, [ tf_port_direction ], [ "var" ], data_type_or_implicit,
		[ port_identifier, { variable_dimension }, [ "=", expression ] ]
	;

tf_port_list =
	tf_port_item, { ",", tf_port_item }
	;

tf_item_declaration =
	block_item_declaration |
	tf_port_declaration
	;

task_body_declaration =
	[ interface_identifier, "." | class_scope ], task_identifier, ";", { tf_item_declaration },
		{ statement_or_null }, "endtask", [ ":", task_identifier ] |
	[ interface_identifier, "." | class_scope ], task_identifier, "(", [ tf_port_list ], ")", ";",
		{ block_item_declaration }, { statement_or_null }, "endtask", [ ":", task_identifier ]
	;

task_declaration =
	"task", [ lifetime ], task_body_declaration
	;

(* See NOTE 9 *)
dpi_task_proto =
	task_prototype
	;

(* See NOTE 8, NOTE 9 *)
dpi_function_proto =
	function_prototype
	;

dpi_task_import_property =
	"context"
	;

dpi_function_import_property =
	"context" | "pure"
	;

dpi_spec_string =
	'"', "DPI-C", '"' | '"', "DPI", '"'
	;

dpi_import_export =
	"import", dpi_spec_string, [ dpi_function_import_property ], [ c_identifier, "=" ], dpi_function_proto, ";" |
	"import", dpi_spec_string, [ dpi_task_import_property ], [ c_identifier, "=" ], dpi_task_proto, ";" |
	"export", dpi_spec_string, [ c_identifier, "=" ], "function", function_identifier, ";" |
	"export", dpi_spec_string, [ c_identifier, "=" ], "task", task_identifier, ";"
	;

function_prototype =
	"function", function_data_type, function_identifier, "(", [ tf_port_list ], ")"
	;

function_body_declaration =
	function_data_type_or_implicit, [ interface_identifier, "." | class_scope ], function_identifier, ";",
		{ tf_item_declaration }, { function_statement_or_null }, "endfunction", [ ":", function_identifier ] |
	function_data_type_or_implicit, [ interface_identifier, "." | class_scope ], function_identifier,
		"(", [ tf_port_list ], ")", ";", { block_item_declaration }, { function_statement_or_null },
		"endfunction", [ ":", function_identifier ]
	;

function_declaration =
	"function", [ lifetime ], function_body_declaration
	;

function_data_type_or_implicit =
	function_data_type |
	[ signing ], { packed_dimension }, [ packed_dimension ] |
	(* See NOTE 23 *)
	type_identifier, [ packed_dimension ]
	;

function_data_type =
	data_type |
	"void"
	;

unsized_dimension =
	"[", "]"
	;

queue_dimension =
	"[", "$", [ ":", constant_expression ], "]"
	;

variable_dimension =
	unsigned_dimension |
	unpacked_dimension |
	associative_dimension |
	queue_dimension
	;

associative_dimension =
	"[", data_type, "]" |
	"[", "*", "]"
	;

(* See NOTE 11 *)
packed_dimension =
	"[", constant_range, "]" |
	unsigned_dimension
	;

unpacked_dimension =
	"[", constant_range, "]" |
	"[", constant_expression, "]"
	;

dynamic_array_new =
	"new", "[", expression, "]", [ "(", expression, ")" ]
	;

(* See NOTE 19 *)
class_new =
	"new", [ "(", list_of_arguments, ")" | expression ]
	;

variable_decl_assignment =
	variable_identifier, { variable_dimension }, [ "=", expression ] |
	dynamic_array_variable_identifier, "[", "]", [ "=", dynamic_array_new ] |
	class_variable_identifier, [ "=", class_new ] |
	(* See NOTE 15 *)
	[ covergroup_variable_identifier ], "=", "new", [ "(", list_of_arguments, ")" ],
	;

limit_value =
	constant_mintypmax_expression
	;

reject_limit_value =
	limit_value
	;

error_limit_value =
	limit_value
	;

pulse_control_specparam =
	"PATHPULSE$", "=", "(", reject_limit_value, [ ",", error_limit_value ], ")" |
	"PATHPULSE$", specify_input_terminal_descriptor, "$", specify_output_terminal_descriptor, "=", "(",
		reject_limit_value, [ ",", error_limit_value ], ")"
	;

type_assignment =
	type_identifier, "=", data_type
	;

specparam_assignment =
	specparam_identifier, "=", constant_mintypmax_expression |
	pulse_control_specparam
	;

param_assignment =
	parameter_identifier, { unpacked_dimension }, "=", constant_param_expression
	;

net_decl_assignment =
	net_identifier, { unpacked_dimension }, [ "=", expression ]
	;

defparam_assignment =
	hierarchical_parameter_identifier, "=", constant_mintypmax_expression
	;

list_of_virtual_interface_decl =
	variable_identifier, [ "=", interface_instance_identifier ],
		{ ",", variable_identifier, [ "=", interface_instance_identifier ] }
	;

list_of_variable_port_identifiers =
	port_identifier, { variable_dimension }, [ "=", constant_expression ],
		{ ",", port_identifier, { variable_dimension }, [ "=", constant_expression ], }
	;

list_of_variable_identifiers =
	variable_identifier, { variable_dimension }, { ",", variable_identifier, { variable_dimension } }
	;

list_of_variable_decl_assignments =
	variable_decl_assignment, { ",", variable_decl_assignment }
	;

list_of_type_assignments =
	type_assignment, { ",", type_assignment }
	;

list_of_tf_variable_identifiers =
	port_identifier, { variable_dimension }, [ "=", expression ],
		{ ",", port_identifier, { variable_dimension }, [ "=", expression ], }
	;

list_of_specparam_assignments =
	specparam_assignment, { ",", specparam_assignment }
	;

list_of_udp_port_identifiers =
	port_identifier, { ",", port_identifier }
	;

list_of_port_identifiers =
	port_identifier, { unpacked_dimension }, { ",", port_identifier, { unpacked_dimension } }
	;

list_of_param_assignments =
	param_assignment, { ",", param_assignment }
	;

list_of_net_decl_assignments =
	net_decl_assignment, { ",", net_decl_assignment }
	;

list_of_interface_identifiers =
	interface_identifier, { unpacked_dimension }, { ",", interface_identifier, { unpacked_dimension } }
	;

list_of_genvar_identifiers =
	genvar_identifier, { ",", genvar_identifier }
	;

list_of_defparam_assignments =
	defparam_assignment, { ",", defparam_assignment }
	;

delay_value =
	unsigned_number |
	real_number |
	ps_identifier |
	time_literal
	;

delay2 =
	"#", delay_value |
	"#", "(", mintypmax_expression, [ ",", constant_mintypmax_expression ], ")"
	;

delay3 =
	"#", delay_value |
	"#", "(", mintypmax_expression, [ ",", mintypmax_expression, [ ",", mintypmax_expression ] ], ")"
	;

charge_strength =
	"(", "small", ")" |
	"(", "medium", ")" |
	"(", "large", ")"
	;

strength1 =
	"supply1" | "strong1" | "pull1" | "weak1"
	;

strength0 =
	"supply0" | "strong0" | "pull0" | "weak0"
	;

drive_strength =
	"(", strength0, strength1, ")" |
	"(", strength1, strength0, ")" |
	"(", strength0, "highz1", ")" |
	"(", strength1, "highz0", ")" |
	"(", "highz1", strength0, ")" |
	"(", "highz0", strength1, ")"
	;

type_reference =
	(* See NOTE 27 *)
	"type", "(", expression, ")" |
	"type", "(", data_type, ")"
	;

struct_union =
	"struct" |
	"union", [ "tagged" ]
	;

data_type_or_void =
	data_type |
	"void"
	;

(* See NOTE 26 *)
struct_union_member =
	{ attribute_instance }, [ random_qualifier ], data_type_or_void, list_of_variable_decl_assignments, ";"
	;

simple_type =
	integer_type |
	non_integer_type |
	ps_type_identifier |
	ps_parameter_identifier
	;

signing =
	"signed" | "unsigned"
	;

var_data_type =
	data_type |
	"var", data_type_or_implicit
	;

variable_port_type =
	var_data_type
	;

(* See NOTE 33 *)
net_port_type =
	[ net_type ], data_type_or_implicit
	;

net_type =
	"supply0" | "supply1" | "tri" | "triand" | "trior" | "trireg" | "tri0" | "tri1" | "uwire" |
	"wire" | "wand" | "wor"
	;

non_integer_type =
	"shortreal" | "real" | "realtime"
	;

integer_vector_type =
	"bit" | "logic" | "reg"
	;

integer_atom_type =
	"byte" | "shortint" | "int" | "longint" | "integer" | "time"
	;

integer_type =
	integer_vector_type |
	integer_atom_type
	;

class_type =
	ps_class_identifier, [ parameter_value_assignment ], { "::", class_identifier, [ parameter_value_assignment ] }
	;

class_scope =
	class_type, "::"
	;

enum_name_declaration =
	enum_identifier, [ "[", integral_number, [ ":", integral_number ], "]" ], [ "=", constant_expression ]
	;

enum_base_type =
	integer_atom_type, [ signing ] |
	integer_vector_type, [ signing ], [ packed_dimension ] |
	(* See NOTE 23 *)
	type_identifier, [ packed_dimension ]
	;

data_type_or_implicit =
	data_type |
	[ signing ], { packed_dimension }
	;

data_type =
	integer_vector_type, [ signing ], { packed_dimension } |
	integer_atom_type, [ signing ] |
	non_integer_type |
	(* See NOTE 12 *)
	struct_union, [ "packed", [ signing ] ], "{", struct_union_member, { struct_union_member }, "}",
		{ packed_dimension } |
	"enum", [ enum_base_type ], "{", enum_name_declaration, { ",", enum_name_declaration }, "}" |
	"string" |
	"chandle" |
	"virtual", [ "interface" ], interface_identifier |
	[ class_scope | package_scope ], type_identifier, { packed_dimension } |
	class_type |
	"event" |
	ps_covergroup_identifier |
	(* See NOTE 28 *)
	type_reference
	;

casting_type =
	simple_type |
	constant_primary |
	signing, [ packed_dimension ] |
	(* See NOTE 23 *)
	type_identifier, [ packed_dimension ]
	;

lifetime =
	"static" | "automatic"
	;

type_declaration =
	"typedef", data_type, type_identifier, { variable_dimension }, ";" |
	"typedef", interface_instance_identifier, ".", type_identifier, type_identifier, ";" |
	"typedef", [ "enum" | "struct" | "union" | "class" ], type_identifier, ";"
	;

(* See NOTE 13 *)
net_declaration =
	net_type, [ drive_strength | charge_strength ], [ "vectored" | "scalared" ], data_type_or_implicit,
		[ delay3 ], list_of_net_decl_assignments, ";"
	;

genvar_declaration =
	"genvar", list_of_genvar_identifiers, ";"
	;

package_import_item =
	package_identifier, "::", identifier |
	package_identifier, "::", "*"
	;

package_import_declaration =
	"import", package_import_item, { ",", package_import_item }, ";"
	;

(* See NOTE 14 *)
data_declaration =
	[ "const" ], [ "var" ], [ lifetime ], data_type_or_implicit, list_of_variable_decl_assignments, ";" |
	type_declaration |
	package_import_declaration |
	virtual_interface_declaration
	;

ref_declaration =
	"ref", variable_port_type, list_of_port_identifiers
	;

interface_port_declaration =
	interface_identifier, list_of_interface_identifiers |
	interface_identifier, ".", modport_identifier, list_of_interface_identifiers
	;

output_declaration =
	"output", net_port_type, list_of_port_identifiers |
	"output", variable_port_type, list_of_variable_port_identifiers
	;

input_declaration =
	"input", net_port_type, list_of_port_identifiers |
	"input", variable_port_type, list_of_variable_identifiers
	;

inout_declaration =
	"inout", net_port_type, list_of_port_identifiers
	;

specparam_declaration =
	"specparam", [ packed_dimension ], list_of_specparam_assignments, ";"
	;

parameter_declaration =
	"parameter", data_type_or_implicit, list_of_param_assignments |
	"parameter", "type", list_of_type_assignments
	;

local_parameter_declaration =
	"localparam", data_type_or_implicit, list_of_param_assignments |
	"localparam", "type", list_of_type_assignments
	;

anonymous_program_item =
	task_declaration |
	function_declaration |
	class_declaration |
	covergroup_declaration |
	class_constructor_declaration |
	";"
	;

anonymous_program =
	"program", ";", { anonymous_program_item }, "endprogram"
	;

package_or_generate_item_declaration =
	net_declaration |
	data_declaration |
	task_declaration |
	function_declaration |
	dpi_import_export |
	extern_constraint_declaration |
	class_declaration |
	class_constructor_declaration |
	parameter_declaration, ";" |
	local_parameter_declaration |
	covergroup_declaration |
	overload_declaration |
	concurrent_assertion_item_declaration |
	";"
	;

package_item =
	package_or_generate_item_declaration |
	anonymous_program |
	(* See NOTE 17 *)
	timeunits_declaration
	;

identifier_list =
	identifier, { ",", identifier }
	;

extern_constraint_declaration =
	[ "static" ], "constraint", class_scope, constraint_identifier, constraint_block
	;

constraint_prototype =
	[ "static" ], "constraint", constraint_identifier, ";"
	;

dist_weight =
	":=", expression |
	":/", expression
	;

dist_item =
	value_range, [ dist_weight ]
	;

dist_list =
	dist_item, { ",", dist_item }
	;

constraint_set =
	constraint_expression |
	"{", { constraint_expression }, "}"
	;

constraint_expression =
	expression_or_dist, ";" |
	expression, "->", constraint_set |
	"if", "(", expression, ")", constraint_set, [ "else", constraint_set ] |
	"foreach", "(", array_identifier, "[", loop_variables, "]", ")", constraint_set
	;

constraint_block_item =
	"solve", identifier_list, "before", identifier_list, ";" |
	constraint_expression
	;

constraint_block =
	"{", { constraint_block_item }, "}"
	;

constraint_declaration =
	[ "static" ], "constraint", constraint_identifier, constraint_block
	;

class_constructor_declaration =
	"function", [ class_scope ], "new", [ "(", [ tf_port_list ], ")" ], ";", { block_item_declaration },
		[ "super", ".", "new", [ "(", list_of_arguments, ")" ], ";" ], { function_statement_or_null },
		"endfunction", [ ":", "new" ]
	;

method_prototype =
	task_prototype |
	function_prototype
	;

(* See NOTE 7 *)
method_qualifier =
	"virtual" |
	class_item_qualifier
	;

(* See NOTE 7 *)
random_qualifier =
	"rand" | "randc"
	;

(* See NOTE 7 *)
property_qualifier =
	random_qualifier |
	class_item_qualifier
	;

(* See NOTE 7 *)
class_item_qualifier =
	"static" | "protected" | "local"
	;

class_constraint =
	constraint_prototype |
	constraint_declaration
	;

class_constructor_prototype =
	"function", "new", "(", [ tf_port_list ], ")", ";"
	;

class_method =
	{ method_qualifier }, task_declaration |
	{ method_qualifier }, function_declaration |
	"extern", { method_qualifier }, method_prototype, ";" |
	{ method_qualifier }, class_constructor_declaration |
	"extern", { method_qualifier }, class_constructor_prototype
	;

class_property =
	{ property_qualifier }, data_declaration |
	"const", { class_item_qualifier }, data_type, const_identifier, [ "=", constant_expression ], ";"
	;

class_item =
	{ attribute_instance }, class_property |
	{ attribute_instance }, class_method |
	{ attribute_instance }, class_constraint |
	{ attribute_instance }, class_declaration |
	(* See NOTE 17 *)
	{ attribute_instance }, timeunits_declaration |
	{ attribute_instance }, covergroup_declaration |
	";"
	;

(* See NOTE 37 *)
program_generate_item =
	loop_generate_construct |
	conditional_generate_construct |
	generate_region
	;

non_port_program_item =
	{ attribute_instance }, continuous_assign |
	{ attribute_instance }, module_or_generate_item_declaration |
	{ attribute_instance }, initial_construct |
	{ attribute_instance }, final_construct |
	{ attribute_instance }, concurrent_assertion_item |
	(* See NOTE 17 *)
	{ attribute_instance }, timeunits_declaration |
	program_generate_item
	;

program_item =
	port_declaration, ";" |
	non_port_program_item
	;

non_port_interface_item =
	generate_region |
	interface_or_generate_item |
	program_declaration |
	interface_declaration |
	(* See NOTE 17 *)
	timeunits_declaration
	;

interface_item =
	port_declaration, ";" |
	non_port_interface_item
	;

extern_tf_declaration =
	"extern", method_prototype, ";" |
	"extern", "forkjoin", task_prototype, ";"
	;

interface_or_generate_item =
	{ attribute_instance }, module_common_item |
	{ attribute_instance }, modport_declaration |
	{ attribute_instance }, extern_tf_declaration
	;

use_clause =
	"use", [ library_identifier, "." ], cell_identifier, [ ":", "config" ]
	;

liblist_clause =
	"liblist", { library_identifier }
	;

cell_clause =
	"cell", [ library_identifier, "." ], cell_identifier
	;

inst_name =
	topmodule_identifier, { ".", instance_identifier }
	;

inst_clause =
	"instance", inst_name
	;

default_clause =
	"default"
	;

config_rule_statement =
	default_clause, liblist_clause, ";" |
	inst_clause, liblist_clause, ";" |
	inst_clause, use_clause, ";" |
	cell_clause, liblist_clause, ";" |
	cell_clause, use_clause, ";"
	;

design_statement =
	"design", { [ library_identifier, "." ], cell_identifier }, ";"
	;

config_declaration =
	"config", config_identifier, ";", design_statement, { config_rule_statement }, "endconfig",
		[ ":", config_identifier ]
	;

bind_instantiation =
	program_instantiation |
	module_instantiation |
	interface_instantiation
	;

bind_target_instance_list =
	bind_target_instance, { ",", bind_target_instance }
	;

bind_target_instance =
	hierarchical_identifier, constant_bit_select
	;

bind_target_scope =
	module_identifier |
	interface_identifier
	;

bind_directive =
	"bind", bind_target_scope, [ ":", bind_target_instance_list ], bind_instantiation, ";" |
	"bind", bind_target_instance, bind_instantiation, ";"
	;

parameter_override =
	"defparam", list_of_defparam_assignments, ";"
	;

non_port_module_item =
	generate_region |
	module_or_generate_item |
	specify_block |
	{ attribute_instance }, specparam_declaration |
	program_declaration |
	module_declaration |
	interface_declaration |
	(* See NOTE 17 *)
	timeunits_declaration
	;

module_or_generate_item_declaration =
	package_or_generate_item_declaration |
	genvar_declaration |
	clocking_declaration |
	"default", "clocking", clocking_identifier,  ";"
	;

module_or_generate_item =
	{ attribute_instance }, parameter_override |
	{ attribute_instance }, gate_instantiation |
	{ attribute_instance }, udp_instantiation |
	{ attribute_instance }, module_instantiation |
	{ attribute_instance }, module_common_item
	;

module_item =
	port_declaration, ";" |
	non_port_module_item
	;

module_common_item =
	module_or_generate_item_declaration |
	interface_instantiation |
	program_instantiation |
	concurrent_assertion_item |
	bind_directive |
	continuous_assign |
	net_alias |
	initial_construct |
	final_construct |
	always_construct |
	loop_generate_construct |
	conditional_generate_construct
	;

ansi_port_declaration =
	[ net_port_header | interface_port_header ], port_identifier, { unpacked_dimension } |
	[ variable_port_header ], port_identifier, { variable_dimension }, [ "=", constant_expression ] |
	[ net_port_header | variable_port_header ], ".", port_identifier, "(", [ expression ], ")"
	;

interface_port_header =
	interface_identifier, [ ".", modport_identifier ] |
	"interface", [ ".", modport_identifier ]
	;

variable_port_header =
	[ port_direction ], variable_port_type
	;

net_port_header =
	[ port_direction ], net_port_type
	;

port_direction =
	"input" | "output" | "inout" | "ref"
	;

port_reference =
	port_identifier, constant_select
	;

port_expression =
	port_reference |
	"{", port_reference, { ",", port_reference }, "}"
	;

port =
	[ port_expression ] |
	".", port_identifier, "(", [ port_expression ], ")"
	;

port_declaration =
	{ attribute_instance }, inout_declaration |
	{ attribute_instance }, input_declaration |
	{ attribute_instance }, output_declaration |
	{ attribute_instance }, ref_declaration |
	{ attribute_instance }, interface_port_declaration
	;

(* See NOTE 25 *)
list_of_port_declarations =
	"(", [ { attribute_instance }, ansi_port_declaration, { ",", { attribute_instance }, ansi_port_declaration } ], ")"
	;

list_of_ports =
	"(", port, { ",", port }, ")"
	;

parameter_port_declaration =
	parameter_declaration |
	data_type, list_of_param_assignments |
	"type", list_of_type_assignments
	;

parameter_port_list =
	"#(", list_of_param_assignments, { ",", parameter_port_declaration }, ")" |
	"#(", parameter_port_declaration, { ",", parameter_port_declaration }, ")" |
	"#(", ")"
	;

timeunits_declaration =
	"timeunit", time_literal, ";" |
	"timeprecision", time_literal, ";" |
	"timeunit", time_literal, ";", "timeprecision", time_literal, ";" |
	"timeprecision", time_literal, ";", "timeunit", time_literal, ";"
	;

package_declaration =
	{ attribute_instance }, "package", package_identifier, ";", [ timeunits_declaration ],
		{ { attribute_instance }, package_item }, "endpackage", [ ":", package_identifier ]
	;

class_declaration =
	[ "virtual" ], "class", [ lifetime ], class_identifier, [ parameter_port_list ],
		[ "extends", class_type, [ "(", list_of_arguments, ")" ] ], ";", { class_item },
		"endclass", [ ":", class_identifier ]
	;

program_declaration =
	program_nonansi_header, [ timeunits_declaration ], { program_item }, "endprogram",
		[ ":", program_identifier ] |
	program_ansi_header, [ timeunits_declaration ], { non_port_program_item }, "endprogram",
		[ ":", program_identifier ] |
	{ attribute_instance }, "program", program_identifier, "(", ".*", ")", ";", [ timeunits_declaration ],
		{ program_item }, "endprogram", [ ":", program_identifier ] |
	"extern", program_nonansi_header |
	"extern", program_ansi_header
	;

program_ansi_header =
	{ attribute_instance }, "program", [ lifetime ], program_identifier, [ parameter_port_list ],
		[ list_of_port_declarations ], ";"
	;

program_nonansi_header =
	{ attribute_instance }, "program", [ lifetime ], program_identifier, [ parameter_port_list ],
		list_of_ports, ";"
	;

interface_declaration =
	interface_nonansi_header, [ timeunits_declaration ], { interface_item }, "endinterface",
		[ ":", interface_identifier ] |
	interface_ansi_header, [ timeunits_declaration ], { non_port_interface_item }, "endinterface",
		[ ":", interface_identifier ] |
	{ attribute_instance }, "interface", interface_identifier, "(", ".*", ")", ";", [ timeunits_declaration ],
		{ interface_item }, "endinterface", [ ":", interface_identifier ] |
	"extern", interface_nonansi_header |
	"extern", interface_ansi_header
	;

interface_ansi_header =
	{ attribute_instance }, "interface", [ lifetime ], interface_identifier, [ parameter_port_list ],
		[ list_of_port_declarations ], ";"
	;

interface_nonansi_header =
	{ attribute_instance }, "interface", [ lifetime ], interface_identifier, [ parameter_port_list ],
		list_of_ports, ";"
	;

module_keyword =
	"module" | "macromodule"
	;

module_declaration =
	module_nonansi_header, [ timeunits_declaration ], { module_item }, "endmodule",
		[ ":", module_identifier ] |
	module_ansi_header, [ timeunits_declaration ], { non_port_module_item }, "endmodule",
		[ ":", module_identifier ] |
	{ attribute_instance }, module_keyword, [ lifetime ], module_identifier, "(", ".*", ")", ";",
		[ timeunits_declaration ], { module_item }, "endmodule", [ ":", module_identifier ] |
	"extern", module_nonansi_header |
	"extern", module_ansi_header
	;

module_ansi_header =
	{ attribute_instance }, module_keyword, [ lifetime ], module_identifier, [ parameter_port_list ],
		[ list_of_port_declarations ], ";"
	;

module_nonansi_header =
	{ attribute_instance }, module_keyword, [ lifetime ], module_identifier, [ parameter_port_list ],
		list_of_ports, ";"
	;

description =
	module_declaration |
	udp_declaration |
	interface_declaration |
	program_declaration |
	package_declaration |
	{ attribute_instance }, package_item |
	{ attribute_instance }, bind_directive |
	config_declaration
	;

source_text =
	[ timeunits_declaration ], { description }
	;

(* Library Map File *)

include_statement =
	"include", file_path_spec, ";"
	;

library_declaration =
	"library", library_identifier, file_path_spec, { ",", file_path_spec },
		[ "-incdir", file_path_spec, { ",", file_path_spec } ], ";"
	;

library_description =
	library_declaration |
	include_statement |
	config_declaration |
	";"
	;

library_text =
	{ library_description }
	;

(*
	Keywords:
		alias
		always
		always_comb
		always_ff
		always_latch
		assert
		assign
		assume
		automatic
		before
		begin
		bind
		bins
		binsof
		bit
		break
		buf
		bufif0
		bufif1
		byte
		case
		casex
		casez
		cell
		chandle
		class
		clocking
		cmos
		config
		const
		constraint
		context
		continue
		cover
		covergroup
		coverpoint
		cross
		deassign
		defparam
		design
		disable
		dist
		do
		edge
		else
		end
		endcase
		endclass
		endclocking
		endconfig
		endfunction
		endgenerate
		endgroup
		endinterface
		endmodule
		endpackage
		endprimitive
		endprogram
		endproperty
		endsequence
		endspecify
		endtable
		endtask
		enum
		event
		expect
		export
		extends
		extern
		final
		first_match
		for
		force
		foreach
		forever
		fork
		forkjoin
		function
		generate
		genvar
		highz0
		highz1
		if
		iff
		ifnone
		ignore_bins
		illegal_bins
		import
		incdir
		include
		initial
		inout
		input
		inside
		instance
		int
		integer
		interface
		intersect
		join
		join_any
		join_none
		large
		liblist
		library
		local
		localparam
		logic
		longint
		macromodule
		matches
		medium
		modport
		module
		nand
		negedge
		new
		nmos
		nor
		noshowcancelled
		not
		notif0
		notif1
		null
		or
		output
		package
		packed
		parameter
		pmos
		posedge
		primitive
		priority
		program
		property
		protected
		pull0
		pull1
		pulldown
		pullup
		pulsestyle_ondetect
		pulsestyle_onevent
		pure
		rand
		randc
		randcase
		randsequence
		rcmos
		real
		realtime
		ref
		reg
		release
		repeat
		return
		rnmos
		rpmos
		rtran
		rtranif0
		rtranif1
		scalared
		sequence
		shortint
		shortreal
		showcancelled
		signed
		small
		solve
		specify
		specparam
		staticand
		string
		strong0
		strong1
		struct
		super
		supply0
		supply1
		table
		tagged
		task
		this
		throughout
		time
		timeprecision
		timeunit
		tran
		tranif0
		tranif1
		tri
		tri0
		tri1
		triand
		trior
		trireg
		type
		typedef
		union
		unique
		unsigned
		use
		uwire
		var
		vectored
		virtualdefault
		void
		wait
		wait_order
		wand
		weak0
		weak1
		while
		wildcard
		wire
		with
		within
		wor
		xnor
		xor
*)
